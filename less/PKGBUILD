# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=less
pkgver=530
pkgrel=1
pkgdesc="A terminal based program for viewing text files"
license=('GPL3')
arch=('i686' 'x86_64')
url="http://www.greenwoodsoftware.com/less"
groups=('base')
depends=('ncurses' 'libpcre')
makedepends=('ncurses-devel' 'pcre-devel')
validpgpkeys=('AE27252BD6846E7D6EAE1DD6F153A7C833235259') # Mark Nudelman
source=("http://www.greenwoodsoftware.com/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::http://www.greenwoodsoftware.com/${pkgname}/${pkgname}-${pkgver}.sig")
sha256sums=('503f91ab0af4846f34f0444ab71c4b286123f0044a4964f1ae781486c617f2e2'
            'SKIP')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
      --build=${CHOST} \
      --prefix=/usr \
      --sysconfdir=/etc \
      --with-regex=pcre
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
